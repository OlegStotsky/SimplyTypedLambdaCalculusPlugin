{
   parserClass="com.olegstotsky.simplytypedlambdacalculus.parser.SimplyTypedLambdaCalculusParser"

   extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

   psiClassPrefix="SimplyTypedLambdaCalculus"
   psiImplClassSuffix="Impl"
   psiPackage="com.olegstotsky.simplytypedlambdacalculus.psi"
   psiImplPackage="com.olegstotsky.simplytypedlambdacalculus.psi.impl"

   elementTypeHolderClass="com.olegstotsky.simplytypedlambdacalculus.psi.SimplyTypedLambdaCalculusTypes"
   elementTypeClass="com.olegstotsky.simplytypedlambdacalculus.psi.SimplyTypedLambdaCalculusElementType"
   tokenTypeClass="com.olegstotsky.simplytypedlambdacalculus.psi.SimplyTypedLambdaCalculusTokenType"

   tokens=[
      BACKSLASH='\'
      ARROW='->'
      PLUS='+'
      MINUS='-'
      LEFT_PAREN='('
      RIGHT_PAREN=')'
      MUL='*'
      DIV='/'
      REM='%'
      LT='<'
      GT='>'
      LTE='<='
      GTE='>='
      EQ='='
      DOT='.'
      COLON=':'
      SEMICOLON=';'

      space='regexp:\s+'
      id='regexp:[a-zA-Z]'
      number='regexp:[0-9]+'
  ]
}

Root ::= Statement *
Statement ::= LambdaExpr ';'
LambdaExpr ::= ApplicationExpr | VariableExpr | AbstractionExpr | CondExpr |  NumExpr | BoolExpr | ParExpr
ApplicationExpr ::= '(' LambdaExpr  LambdaExpr ')'
AbstractionExpr ::= '\' id ':' TypingExpr '.' LambdaExpr
CondExpr ::= if LambdaExpr then LambdaExpr else LambdaExpr
TypingExpr ::= Int TypingExprOther | Bool TypingExprOther | Int | Bool
TypingExprOther ::= '->' TypingExpr
ParExpr ::= '(' LambdaExpr ')'
BoolExpr ::= true | false
NumExpr ::= number
VariableExpr ::= id
